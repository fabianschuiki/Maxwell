<!DOCTYPE html>
<html>
<head>
	<title>Maxwell – Ideas</title>
	<meta charset="utf-8"/>
	<script src="http://code.jquery.com/jquery-1.6.2.min.js" type="text/javascript"></script>
	<script src="include/include.js" type="text/javascript"></script>
	<link rel="stylesheet" href="include/include.css" type="text/css" />
</head>
<body>

<h1>Packages</h1>
<p>Source code written in Maxwell should be organized into individual packages. Other parts of the project and potentially other programmers using the package will refer to its contents using the package name.</p>
<p>Consider the following dummy package:</p>
<code>/* somepkg/utils.mw */
func greet { console.writeln("Hello World"); }</code>
<p>And the following simple console program trying to make use of it:</p>
<code>/* hello.mw */
func main { greet(); }</code>
<p>Compiling <code>hello.mw</code> will fail since <code>greet()</code> is unknown. The compiler has to be told where to find the function:</p>
<code>/* hello.mw */
import "somepkg";
func main { somepkg.greet(); }</code>

<h2>File Dependencies</h2>
<p>Packages introduce file dependencies. The compiler needs to be able to tell at an early stage what other files a source file might refer to and what names that file might contain. A distinction has to be made between known entity names that are located in other files and the entities that a certain entity depends upon.</p>
<p>Consider the following file hierarchy and files:</p>
<p><ul>
	<li>hello.mw</li>
	<li>world.mw</li>
	<li>somepkg/
	<ul>
		<li>foo.mw</li>
		<li>bar.mw</li>
	</ul>
	</li>
</ul></p>
<code>/* hello.mw */
func main { greetWorld(); }

/* world.mw */
import "somepkg"
func greetWorld { somepkg.someFoo(); }

/* somepkg/foo.mw */
func someFoo { console.writeln("Hello " + someBar()); }

/* somepkg/bar.mw */
func someBar -> (String s) { return "World"; }</code>

<p>The command used to compile the program is:</p>
<code>mwc hello.mw</code>

<p>The compiler's first step is to determine what files need to be parsed in order to be able to see all the entity names that the programmer might refer to. First, all files in the same directory as the main source file are parsed, namely <code>hello.mw</code> and <code>world.mw</code>. This generates the following raw, not yet analyzed entities which are added to the main file's list of <em>known</em> entities:</p>
<code>#1 func main
#2 func greetWorld</code>
<p>The compiler now starts its name-resolving process by iterating through the main file and resolving all the name references. Since it encounters the <code>greetWorld()</code> function, the compiler resolves this call to the <code>func greetWorld</code> function defined in <code>world.mw</code>. This will add entity #2 to the list of <em>required</em> entities of entity #1.</p>
<p>Since entity #2 is a dependency of the main file, the compiler will continue name-resolving by iterating through the <code>world.mw</code> file and collecting all <code>import</code> statements. All import statements are resolved to package locations in the file system, and the compiler will parse all source files contained therein. This generates the following raw entities in <code>world.mw</code>'s list of <em>known</em> entities:</p>
<code>#3 func someFoo
#4 func someBar</code>
<p>The compiler then continues its name-resolving by iterating through entity #2 and resolving all name references. This will resolve the call to <code>somepkg.someFoo()</code> call to the <code>func someFoo</code> function in the <code>somepkg</code> package. This will add entity #3 to the list of <em>required</em> entities for entity #2.</p>
<p>Since entity #3 is now a dependency of entity #2, the compiler will also resolve its name references. Since no packages are imported, the only names that are known are <code>someFoo</code> and <code>someBar</code>. The compiler will resolved the cal to <code>func someBar</code>, which adds entity #4 as a dependency of entity #3.</p>

<p>Since all files have been parsed and the names resolved, the compiler will build the canonical dependency tree of the entities, which looks as follows:</p>
<code>#4 depends on nothing
#3 depends on 4
#2 depends on 3, 4
#1 depends on 2, 3, 4</code>
<p>All analysis and compilation steps are now performed as a task queue with the entire dependency tree loaded as queue at the beginning. This yields the general analysis and compilation order <code>#4,#3,#2,#1</code>.</p>

<h2>Influences on Compilation</h2>
<p>Since a canonical dependency tree exists, the compiler will perform a check at each stage whether a certain file has been modified. If it has, the file will be run through the parser again. This change will remove all the compiled information for the dependent files whic will cause the compiler to re-compile these files as well.</p>
<p>Not importing entire files or directories at once, but rather building the dependency tree from the actual name usage will result in the compiler only processing the files that are actually necessary. A source file may import a <em>huge</em> package containing many files and the compiler will only have to tokenize and parse them once in order to build the list of names. After that, only the actual entities from the package that are used are processed.</p>
<p>Since the precise dependency tracking requires lists of files and their contained entities, the result of the compilation process may be stored as individual source snippet XMLs. A final <em>synthesizer</em> stage will then collect all the snippets that belong to one of the original source files and will fuse them into a <code>.h</code> and <code>.c</code> file. This makes reading the output much more clear.</p>

<h2>A Note on Specializations</h2>
<p>Since the concept of generic types requires the compiler to copy and specialize functions and types during compilation, these specializations have to be listed in the original entity's file. These specializations may then be added as <em>required</em> and <em>known</em> entities to other entities as well.</p>



<h1>Project Configuration</h1>
<p>To make using the Maxwell compiler from the command line as easy as possible, it might be desirable to employ a mechanism similar to the one <em>git</em> uses for its data structure.</p>
<p>There should be <em>Maxwell configuration files</em> that can be distributed arbitrarily in the source tree, which affect their sibling files and all subdirectories. These config files might contain certain optimization information, whether to build in debug mode, what C libraries to use automatically, etc..</p>
<p>The project root should have a <code>.maxwell</code> or <code>.mwc</code> directory that contains the intermediate build files (tokens, AST, entities, etc.). Using the Maxwell compiler would then boil down to calling <code>mwc</code> from anywhere in the source tree. The compiler would automatically walk up the tree to the next <code>.mwc</code> directory and look for build configurations there.</p>

<h2>Compiler Interrogation</h2>
<p>This scheme would allow for efficient compiler interrogation. That is the user beeing able to ask the compiler for certain information after the compilation process is complete. This is important for a language where the compiler makes a fair amount of decisions for the source code. The user might ask the compiler what <em>C type</em> the variable <code>var Vector v</code> has. Depending on certain factors, the compiler might choose to go for <code>struct Vector* v</code> or <code>struct Vector v</code>. Or the compiler might decide to make certain function calls or member accesses thread safe, or to make certain assumptions.</p>
<p>The user should be able to retrieve this information from the compiler, since it is vital during the optimization stage of the project. The compiler might have chosen the wrong implementation of <code>Array</code>, maybe a single-linked list instead of a vector. Being able to retrieve this information helps to nail down performance issues which may be eliminated through compiler hints.</p>

<h2>Documentation</h2>
<p>Having this <em>build repository</em>, the compiler may produce valuable documentation from the individual source files which the user may not only export but might also want to be able to retrieve online, by asking the compiler questions.</p>
<p>With generic programming and type inference the type of a function is usually more restricted after compilation than the user initially wrote down himself. Writing a function that calculates the norm of something will usually boil down to a function that takes an argument for which a <em>sqrt</em> function is defined. This information may be made visible in such a documentation: Instead of having norm(x) with x anything, the documentation would actually say norm(x) with x anything such that sqrt(x) is defined.</p>



<h1>If Expression</h1>
<p>It should be possible to have complex if statements as expressions, similar to how cases are notated in mathematics.</p>
<code>a.doSomethingWith({0 if x == "null", 1 otherwise});</code>
<p>Or, in a more elaborate case:</p>
<code>var int x = getUserInput();
var String b = {
	"0" if x == 0,
	"1" if x == 1,
	">1" otherwise };</code>


<h1>"incase" Statement/Expression</h1>
<p>The <em>incase</em> keyword should introduce a construct that implements and extends the well-known <em>switch and case</em> statement. The keyword may be used as a statement, taking statements or blocks as case arguments; or as an expression, taking any expressions as case arguments. The construct allows a sequence of binary operator expressions with the same left hand side operand and optionally the same operator to be written in a compact manner.</p>
<code>var int x = getUserInput();
if (x == 0)
	doA();
if (x == 1)
	doB();
if (x == 2)
	doC();
</code>
<p>In the preceding code, all <em>if</em> statements share the same LHS operand and operator, namely <code>x ==</code>. Using the <code>incase</code> keword, these statements may be simplified to:</p>
<code>incase x == 0: doA(), 1: doB(), 2: doC();

// or easier to read on multiple lines
incase x ==
	0: doA(),
	1: doB(),
	2: doC();</code>

<p>In case the LHS is the same but the operator changes:</p>
<code>incase x == 0: doA(), &lt;= -1: doB(), &gt;= 1: doC();

// or easier to read on multiple lines
incase x
	== 0: doA(),
	&lt;= 1: doB(),
	&gt;= 1: doC();</code>

<p>To make it possible to contract <em>if/else if/else</em> constructs, the <code>otherwise</code> keyword allows for the <em>else</em> part:</p>
<code>incase x == 0: doA(), 1: doB(), otherwise: doC();</code>


<h1>Option Types</h1>
<p>The planned type inferrence strongly suggests the support of option types, i.e. a special type that is a set of actual types. This allows a variable to be expressed as being either of type "A or B".</p>
<code>var String|Error r = getUserInput();</code>

<h2>Implications</h2>
<p>One implication of option types is that it allows for very rigorous type checking. If <em>String</em> has a function <code>getLength()</code> and <em>Error</em> a function <code>getMessage()</code>, the statement…</p>
<code>r.getLength();
r.getMessage();</code>
<p>…will throw an error, since neither of the two functions is supported by both types. In case there is some powerful assumption logic in place, the following would work:</p>
<code>if (r isa String) {
	//In this scope, r is of type String.
	r.getLength();
}
if (r isa Error) {
	//In this scope, r is of type Error.
	r.getMessage();
}</code>



<h1>String Formatting</h1>
<p>In general, there should be a function that allows a string to be formatted by replacing special template characters (e.g. <em>%s</em>) with appropriate values from a value map or array. Syntactic sugar should be available to make this process as easy as possible:</p>
<code>var int x = 1;
var float y = 1.2345;
var String text = "We have an int %{x}, some float %{y} and a float as int %i{x}.";
</code>
<p>The last line of the snippet should automatically be converted to:</p>
<code>var String text = "We have an int %i, some float %f and a float as int %i.".format([x, y, x]);</code>



<h1>Nullable Types</h1>
<p>Variables with an object type should not automatically accept <code>null</code> or <code>nil</code> as a value, even if the C backend has nullable pointers for free. This enables the programmer to clearly distinguish functions that always return a value and those that may return null.</p>
<code>func loadWebsite -> (<span class="type">String|null</span>) { ... } //This function returns the contents of the website on success, or null if an error occurred.

var String google = loadWebsite(); //Fails to compile, since the variable mustn't be null.
var String|null google = loadWebsite(); //Works.</code>

<h2>Implications</h2>
<p>Combined with type inferrence and some powerful assumption logic, this allows for some nice error checking that may catch a lot of the classic <em>null-pointer dereferencing</em> issues at compile time:</p>
<code>//loadWebsite() as above
var any google = loadWebsite(); //Type detected to be String|null.

//The following will fail and prevent a null-dereferencing segfault…
var int length = google.length(); //…since no functions can be called on null.

//The following will work…
if (google == null)
	return;
var int length = google.length(); //…since the compiler assumes the variable to be of type String after the if-statement.</code>



<h1>Interfaces / Typeclasses</h1>
<p>Due to its generic nature, Maxwell only imposes those constraints on entity types that are vital for the program's integrity. Consider the following snippet of code:</p>
<code>func describeWeight(thing) {
	if (thing.weight &lt; 20)
		return "light";
	else
		return "heavy";
}</code>

<p>When compiling, Maxwell would infer the type of <code>thing</code> to be anything that has a <code>weight</code> property. Typeclasses allow the programmer to explicitly restrict a generic type to meet a minimal standard of functionality that is required. The function above may be written more explicitly:</p>
<code>//define a "weighable" object as something that has a weight assigned
typeclass #weighable { var any weight; };

//our function now describes the weight of something that is weighable
func describeWeight(thing #weighable) { ... }</code>

<p>Typeclasses look like a Twitter hashtag as they categorize an entity. Other definition syntaxes might look like:</p>
<code>typeclass colored { ... }
typeclass #colored { ... }

tag colored { ... }
tag #colored { ... }

interface colored { ... }
interface #colored { ... }</code>

<h2>Simple Example</h2>
<p>Having these hashtags helps improve the readability of Maxwell code and simplifies the task of analyzing what a certain snippet of code is doing. Consider the following snippet of code. For this section of the code, it is not important that <code>connection.receive()</code> returns a set of packets, but rather that it supports the <code>count()</code> and <code>get(var i)</code> functions, as they're required for the iteration.</p>
<code>//we've setup a network connection where we receive packets
var any packets #countable = connection.receive();

//we don't care that these are packets, we just want to iterate over them
for (var i = 0; i &lt; packets.count(); i++) {
	var any packet = packets.get(i);
	//... do something with it
}</code>

<h2>Nested Tags</h2>
<p>Tags become even more useful if they may be nested and defined across multiple hierarchy levels. Consider the following example, in which <code>list</code> may be any collection whose objects are <code>#named</code>:</p>
<code>typeclass #named { var name; }

//we want to process an collection of objects whose name we're going to require
var any list [#named] = connection.receive();
for (var obj in list) {
	console.print("received something called %{obj.name}\n");
	//...
}</code>

<p>Or the following example with objects:</p>
<code>typeclass #rgb { var r; var g; var b; }
typeclass #colored { var color; }

var any mesh #{color #rgb} = engine.getMesh();
mesh.color.r = 1.0;
mesh.color.g = 0.5;
mesh.color.b = 0.0;

//or more Maxwell-like:
{ c.r = 1.0; c.g = 0.5; c.b = 0.0; } where c = mesh.color;

//or even more Maxwell-like:
mesh.color.(r,g,b) = (1.0, 0.5, 0.0);</code>



<h1>In-case-null abbreviation</h1>
<p>In C and C++ you tend to do things like:</p>
<code>Person *p = fetchPerson();
return (p ? p->getName() : "unknown");</code>

<p>In Maxwell the following should be possible as an abbreviation, with the latter form to be preferred as it describes its operation better:</p>
<code>return (fetchPerson() ?? "unknown");
return (fetchPerson() or "unknown");</code>



<h1>Types</h1>

<h2>Member Accessors</h2>
<p>Consider the following type:</p>
<code>type Person {
	var String name;
	var int age;
}</code>

<h3>Direct Access</h3>
<p>Accessing the members of the type should be as straightforward and easy as possible.</p>
<code>var Person p = somePerson();
var String n = p.name;
p.name = "John Doe";</code>

<p>This code yields the following C code:</p>
<code>struct Person {
	struct String *name;
	int age;
};
struct Person *p = somePerson();
struct String *s = p->name;
p->name = String_new("John Doe");</code>

<h3>Synchronized Direct Access</h3>
<p>If the analyzer detects access to variable through multiple threads, it will protect the access through locks:</p>
<code>struct Person {
	struct String *name;
	int age;
	pthread_mutex_t name_mutex;
	pthread_mutex_t age_mutex;
};
struct Person *p = somePerson();

pthread_mutex_lock(&amp;p->name_mutex);
struct String *s = p->name;
pthread_mutex_unlock(&amp;p->name_mutex);

pthread_mutex_lock(&amp;p->name_mutex);
p->name = String_new("John Doe");
pthread_mutex_unlock(&amp;p->name_mutex);</code>

<h3>Accessors</h3>
<p>The programmer may choose to provide their own setter and getter methods, in which case all member accesses will be performed through the accessors.:</p>
<code>struct Person *p = somePerson();
struct String *s = Person_get_name(p);
Person_set_name(p, String_new("John Doe"));</code>

<p>Member accesses inside their own accessor function will be performed through one of the <em>Direct Access</em> methods.</p>
<code>func name= (Person p, String s) { p.name = s; }
func main
{
	var Person p = somePerson();
	p.name = "John Doe";
}</code>
<p>The previous code snippet will produce the following C code:</p>
<code>void Person_set_name(struct Person *p, struct String *s)
{
	p->name = s;
}
void main()
{
	struct Person *p = somePerson();
	Person_set_name(p, String_new("John Doe"));
}</code>


<h2>Values vs. Objects</h2>
<p>Maxwell shall distinguish between values and objects, i.e. entities that are represent magnitude and identity.</p>

<h3>Objects</h3>
<p>Objects are entities that you instinctively assign an identity to. A person for example has a unique identity. Duplicating a person yields another entity which is not identical to the original.</p>
<code>object Person {
	var String name;
	var int age;
}</code>

<p>Objects are treated as allocated memory locations in the resulting C code.</p>
<code>func main
{
	var Person p = new Person;
	p.name = "John Doe";
	
	var Person q = p;
	q.age = 25;
	
	//p and q both represent the same Person, namely John Doe, age 25.
}</code>

<p>The previous code yields the following C code:</p>
<code>struct Person {
	struct String name;
	int age;
};

void main()
{
	struct Person *p = Person_new();
	p->name = String_new("John Doe");
	struct Person *q = p;
	q->age = 25;
}</code>

<h3>Values</h3>
<p>Values are entities that have no identity and usually represent some form of magnitude or value. A vector for example has no identity. Two vectors with equal components are considered to be equal.</p>
<code>value Vector {
	var float x;
	var float y;
}</code>

<p>Values are treated as structs that are copied around from function to function in the resulting C code.</p>
<code>func main
{
	var Vector v;
	v.x = 1;
	
	var Vector w = v;
	w.y = 2;
	
	//v is a vector (1,0), w a vector (1,2).
}</code>

<p>The previous code yields the following C code:</p>
<code>struct Value {
	float x;
	float y;
};

void main()
{
	struct Vector v = Vector_new();
	v.x = 1;
	struct Vector w = v;
	w.y = 2;
}</code>

<h3>Example: String and Data</h3>
<p>A string is usually considered a value. It represents a bunch of text. Copying around thousands of bytes for each string is inefficient though. This is where the value/object distinction comes in handy: A string may well be considered a value, the memory block containing the string data is not! A block of memory is unique and has an identity, two memory blocks with the same content are not equal, as they may cover different areas of the system memory.</p>
<p>The concept of a string may be implemented as follows:</p>
<code>value String {
	var Blob bytes;
}

object Blob {
	var $unsigned char* data;
	var int size;
}</code>


<h1>Expressions vs. Statements</h1>
<p>Maxwell should not make a distinction between expressions and statements, as C, C++ or Java do. Statements in general do not have a return type and value, whereas expressions represent an operation having a return type and value. Instead, everything in Maxwell is an expression and has a return type.</p>
<ul>
	<li><strong>Type Definition</strong> returns the defined type itself.</li>
	<li><strong>Function Definition</strong> returns the defined function object. This is useful since Maxwell should support anonymous functions that have no name, in which case getting a grip on the function object is only possible through the returned value of the function definition.</li>
	<li><strong>Blocks</strong> count as an expression themselves. A block consists of a list of expressions with a semicolon at the end of each. The returned value of the block is the block's last expression.</li>
	<li><strong>If Expressions</strong> return the value of the branch taken. Since the branches usually are blocks, the last expression of the if or else block is returned. The return type equals the union type of the if and else branch blocks.</li>
	<li><strong>For Expressions</strong> return their body after the last iteration. Since the body is usually a block expression, the last expression in the block with the parameters of the last iteration is returned.</li>
</ul>

<h2>Return nil for Statements</h2>
<p>The absence of a return value or type is denoted with the <code>nil</code> type. Functions without any outputs return <code>nil</code>.</p>

<h2>Examples</h2>
<code>// Returns a function object
func main {...}

// Returns a type object
type Foo

// Returns the integer 6
{
	var x = 5;
	x + 1; // last expression, returned
}

// Returns either "Hello" or 123, based on whether x is 0. The return type is String|Int.
if (x == 0) {
	"Hello";
} else {
	123;
}

// Returns 9, since the last iteration is x = 3.
for (var x Int = 0; x &lt; 4; x++) {
	x*x;
}
</code>


<h1>Complex For Loops</h1>
<p>Maxwell shall support Scala-style for loops with complex iteration behaviour. This allows multiple nested for loops to be expressed in one for expression with a more elaborate loop configuration block.</p>
<code> // For each x from 0 to 9, loop through all y between 0 to 9 that are not twice as big as x.
for (var x Int = 0; x &lt; 10; x++) {
	for (var y Int = 0; y &lt; 10; y++) {
		if (y == x*2) continue;
		// do stuff
	}
}

// With a complex for loop, this can be collapsed into one.
// The first block configures the looping behaviour, i.e. it performs the filtering
// on the possible iterations. The second block is the actual iteration body that is
// called for each loop iteration.
for {
	x &lt;- 0 to 9;
	y &lt;- 0 to 9;
	if (y == x*2) continue;
} {
	// do stuff
}
</code>


<h1>Anonymous Functions</h1>
<p>Maxwell shall have good support for anonymous and inline functions. This should make code much more readable and should allow for on-the-fly callbacks.</p>
<code>var f = func (x Int) -> (y Int) {
	y = x * 2;
}
f(1); // returns 2
</code>

<h2>Callback Variables</h2>
<p>Anonymous function bodies should have access to variables defined outside their scope. The compiler should check what variables of the outer scope are referenced, and implicitly add all it finds to the anonymous function's inputs. If the function is used as a callback, the callback should properly bind the variable's values to the implicit function inputs, such that handling of the function feels natural.</p>
<code>func main
{
	var x Int = 123;
	var y String = "Hello";

	// Define an anonymous function that references x and y.
	var f = func (z Int) {
		x.print; y.print; z.print;
	};

	// This is equivalent to the following:
	var f0 = func (x Int, y String, z Int) {
		x.print; y.print; z.print;
	};
	var f = f0.bind({"x": x, "y": y});

	// The referenced variables are bound at the moment the function is defined.
	x = 234;
	y = "World";
	f(0); // prints 123, Hello and 0.
}
</code>



<h1>Tuples</h1>
<p>Tuples are first-class citizens of Maxwell. They form the input and output of functions and allow for parallel assignments and more. A tuple combines traits of arrays and map in that its fields are ordered and indexed (0,1,2,…) as in the former, yet also named ("a", "b", …) as in the latter. A tuple in general contains one or more fields. Tuples with only one field are implicitly convertible to their single field's type. The <code>nil</code> type/value should be used as a placeholder for empty tuples.</p>
<code>// Variable with a tuple type and an initial tuple expression.
var x (Int, Int) = (1, 2);

// It should be possible to drop the paranthesis most of the time.
var x (Int, Int) = 1, 2;
</code>

<p>Tuples provide access to their fields through indexes and strings, as well as through member accessors. They provide additional functions that make using them feel like using maps or arrays. A tuple also provides an implicit cast to its map and array equivalent.</p>
<code>var x = (a: 6, b: 7, c: 8);

// The following lines of expressions are all equivalent:
x[0]; x[1]; x[2];
x["a"]; x["b"]; x["c"];
x.a; x.b; x.c;

Array(x); // returns [6,7,8]
Map(x); // returns {"a": 6, "b": 7, "c": 8}

// Tuples shall also provide the following convenience functions that provide a container-like interface:
x.empty;
x.length;
</code>

<h2>Parallel Assignment</h2>
<p>Using a tuple as the left hand side of an assignment will match assign the right hand side to all fields of the tuple, or if the right hand side is a tuple as well, will assign the corresponding fields.</p>
<code>var x Int;
var y Int;
(x,y) = (1,2);
</code>
<p>This should be implemented by spawning an AssignmentExpr for each pair of tuple fields. This should also deactivate the root AssignmentExpr that assigns the tuple to the other tuple, since the type system will try to cast the tuple otherwise. Spawning "sub-assignments" has the powerful side effect of allowing nested parallel assignments:</p>
<code>var x Int;
var y Int;
var z Int;
(x,(y,z)) = (1,(2,3));
</code>

<h2>Implicit Tuple Operators</h2>
<p>If an operator is used on two matching tuples but no implementation of the operator can be found, the compiler should implicitly spawn a BinaryOpExpr for each pair of tuple fields. If the binary operator has a tuple on its LHS, but not on its RHS, the spawned operators will all use the same RHS. This allows the user to assign the same value to all tuple fields at once. This should, as was the case in the parallel assignment, deactivate the root BinaryOpExpr to avoid type system trouble. These "sub-operators" again provide the side effect of nesting:</p>
<code>(5,(6,7)) + (1,(2,3)); // returns (6,(8,10))
(1,2,3) + 5; // returns (6,7,8)
</code>
<p>It is important that the compiler first tries to find an implementation of the operator. This allows the user to write custom and/or more efficient versions of tuple operators.</p>

<h2>Finding Field Pairs</h2>
<p>If a tuple is to be assigned to another tuple, or a tuple is to be used as a list of function arguments, the individual fields of one tuple need to be mapped to the fields of the other tuple. This is accomplished by first mapping all unnamed fields to the corresponding <strong>index</strong> and secondly mapping all named fields to the corrseponding <strong>field name</strong> of the other tuple.</p>
<p>This implies that the mapping between two tuples of equal length may have less mapped fields than the tuples are long. In general it will be good practice to hardly ever mix named and unnamed tuples. The mapping of tuple fields should be wrapped and stored in a separate object. If two tuples and the mapping from one to the other have the same length, the tuples are called <strong>matching</strong>.</p>

<code>// The following calls the generic + function for tuples which is detailed below.
(1,2) + (3,4);
// This calls the following function, specializing it for (Int,Int) input tuples.
func + (a Tuple, b Tuple) -> (c Tuple) { ... }
func + (a (Int,Int), b (Int,Int)) = (a[0] + b[0], a[1] + b[1]); // specialized
</code>




<h1>Random Code Snippets</h1>

<h2>Errors/Exceptions</h2>
<code>type Shader
{
	var path;
	var shader;
	
	func compile
	{
		//Open the file containing the shader code.
		var f = File.open(path);
		defer f.close();
		
		//Read the entire data.
		var code = f.read();
		var size = code.size;
		
		//Assign the code to the shader and compile.
		glShaderSource(shader, 1, code, size);
		glCompileShader(shader);
		
		//Check whether compilation went well.
		var compiled;
		glGetShaderiv(shader, GL_COMPILE_STATUS, compiled);
		
		//Throw an error if not.
		if (!compiled) {
			var l = 0;
			glGetShaderiv(shader, GL_INFO_LOG_LENGTH, l);
			var GLchar[l] msg;
			glGetShaderInfoLog(shader, l, l, msg);
			throw Error (msg);
		}
		
		//In case we encounter an error.
		error {
			throw Error ("Unable to compile shader %{path.name}.");
		}
	}
}</code>

<p>Running the above code with a path that does not point to a file (e.g. <code>"nowhere/test.frag"</code>), the <code>File.open()</code> factory will throw an error <code>"File nowhere/test.frag does not exist."</code>. Once this happens, the error handler will get called, with the file-not-found error stored in the <em>environment</em>. The error handler creates another Error object which will use the existing error in the environment as its <em>underlying error</em>. Converting the new error to text would yield <code>"Unable to compile shader test.frag. File nowhere/test.frag does not exist."</code>.</p>


<h2>Error Handling in Files</h2>
<p>When trying to open a file that does not exist, the <code>File</code> factory may either return <code>null</code> or throw an error, depending on what you're requested return type is:</p>
<code>//The following will throw an error if the file doesn't exist:
var f = File.open("test.txt"); //f inferred to be of type "File"
f.read();

//The following will return null if the file doesn't exist:
var f = File.open("test.txt"); //f inferred to be of type "File|null"
if (f == nil) return "failed!";
f.read();</code>

<p>Implementation of <code>File.open</code>:</p>
<code>type File
{
	func open (path) -> (r)
	{
		if (!File.exists(path)) {
			if (r maybe nil) {
				return nil;
			} else {
				throw Error ("File %{path} does not exist.");
			}
		}
	}
}</code>
<p>The <code>maybe</code> operator returns <code>true</code> if the expression on the left hand side may be of the type on the right hand side. In case of <code>File</code> it would return <code>false</code>, in case of <code>File|null</code> it would return <code>true</code>.</p>


<h2>Deferred Statements</h2>
<p>Inspired by the Go language, the <code>defer</code> keyword introduces a statement that is executed when the function is about to be terminated, either due to a <code>return</code> or a <code>throw</code>.</p>
<code>func read
{
	var f = File.open("test.txt");
	defer f.close();
	
	if (f.size &lt; 100) {
		throw Error ("File %{f.path} too small.");
		//f.close() will be executed here
	}
	
	return f.read(100);
	//f.close() will be executed here
}</code>


<h2>Factories vs. Constructors</h2>
<p>Instead of constructors as in C++ it would be more convenient to have factory functions for types. This would make code more readable and the code inside the factory would not have the restrictions that apply to C++ constructors (e.g. no virtual function calls, etc.).</p>

<code>type Vector
{
	var x;
	var y;
	var otherStuff;
	
	func new
	{
		var same v;
		v.x = 0;
		v.y = 0;
		v.otherStuff = nil;
		return v;
	}
	
	func new (x,y)
	{
		var same v = new(); //does basic initialization
		v.x = x;
		v.y = y;
		return v;
	}
}</code>


<h2>Function Argument "Don't Care" Order</h2>
<p>For certain functions, function argument order is irrelevant, i.e. <code>add(a,b)</code> and <code>add(b,a)</code> is equivalent. It should be possible to tell the compiler about this when defining a function.</p>

<code>func multiply (Vector v', float f') -> (Vector r)
{
	r = v*f;
}

//allows the following code to work
var Vector v;
var float f;
multiply(v,f);
multiply(f,v);</code>


<h2>Short Function Definition</h2>
<p>Small functions can be defined quickly by omitting the return type and instead adding a <code>=</code> and an equivalent expression. The function will then use the expression's type as its return type. Omitting the <code>=</code> symbol will define a regular function with no outputs, which will return nil.</p>
<code>// Return type of squared is the same as that of the * operator for the type of x.
func squared (x) = x*x;
// Return type of norm is the same as that of sqrt.
func norm (x) = { var x2 = x*x; sqrt(x); }
// Return type of foo is nil.
func foo (x) { sqrt(x); }
</code>

</body>
</html>